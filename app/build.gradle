buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath rootProject.ext.plugin.retroLambda
        classpath rootProject.ext.plugin.grgit
    }
}

import org.ajoberstar.grgit.Grgit

ext {
    git = Grgit.open(currentDir: projectDir)
    gitVersionName = git.describe()
    gitVersionCode = git.tag.list().size()
    gitVersionCodeTime = git.head().time
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'android-apt'
apply plugin: 'com.neenbedankt.android-apt'

android {

    compileSdkVersion rootProject.ext.config.compileSdkVersion
    buildToolsVersion rootProject.ext.config.buildToolsVersion

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileName = "hitracker-" + variant.productFlavors[0].name + ".apk"
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(output.outputFile.name, fileName))
        }
    }

    defaultConfig {
        applicationId "org.hisp.india.trackercapture"
        minSdkVersion rootProject.ext.config.minSdkVersion
        targetSdkVersion rootProject.ext.config.compileSdkVersion
        versionCode gitVersionCode
        versionName gitVersionName
        renderscriptTargetApi rootProject.ext.config.renderscriptTargetApi
        renderscriptSupportModeEnabled true
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("keystores/keystore-debug.jks")
            storePassword 'android'
        }
        release {
            keyAlias 'HITRACKER'
            keyPassword 'org.hisp.india.trackercapture'
            storeFile file("keystores/keystore-release.jks")
            storePassword 'org.hisp.india.trackercapture'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {

        dev {
            versionCode gitVersionCodeTime
            versionName gitVersionName
            manifestPlaceholders = [appName: "Tracker-" + "dev"]
        }

        prod {
            versionCode gitVersionCode
            versionName gitVersionName
            manifestPlaceholders = [appName: "Tracker-" + "prod"]
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

}

apt {
    arguments {
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
        resourcePackageName android.defaultConfig.applicationId
    }
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = ['--multi-dex']
        } else {
            dx.additionalParameters += '--multi-dex'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile project(':hi-core')

    def dependency = rootProject.ext.dependency
    //Android support
    compile dependency.supportMultidex
    compile dependency.appcompatv7
    compile dependency.supportDesign
    compile dependency.supportv4
    compile dependency.recyclerviewv7

    //Dependencies Injection
    apt dependency.androidAnnotationCompiler
    compile dependency.androidAnnotation
    //Dagger
    apt dependency.daggerCompiler
    compile dependency.dagger
    provided dependency.generatedAnnotation

    //MVP
    compile dependency.mvp
    compile dependency.mvpViewState
    //Adapters
    compile dependency.baseAdapter
    //UI Validations
    compile dependency.saripaar
    //Fonts
    compile dependency.calligraphy
    //Messages aka EventBus
    compile dependency.eventBus
    //Permission
    compile dependency.npermission

}
